;; Advent of code day 14
(ql:quickload '(:alexandria :serapeum))

(defparameter *input-string* (string-trim '(#\Newline) (alexandria:read-file-into-string "input.txt")))
(defparameter *size* 128)

(defun repeat (sequence times)
  (apply #'concatenate 'list (make-list times :initial-element sequence)))

(defun arange (limit)
  (make-array limit :initial-contents (alexandria:iota limit :start 0)))

(defun reverse-subarray (array from len)
  (let ((array (alexandria:rotate array (- from)))
        (slice (subseq array 0 len)))
    (setf (subseq array 0 len) (nreverse slice))
    (alexandria:rotate array from)))

(defun update-array (array lengths &optional (pos 0) (skip 0))
  (if (null lengths) array
      (update-array (reverse-subarray array pos (car lengths))
                    (cdr lengths) (+ pos (car lengths) skip) (+ skip 1))))

(defun knot-hash (input)
  (let* ((lengths `(,@(map 'list #'char-code input) 17 31 73 47 23))
         (numbers (arange 256))
         (results (update-array numbers (repeat lengths 64))))
    (format nil "铂О" (mapcar (lambda (slice) (reduce #'logxor slice)) (serapeum:batches results 16)))))

(defun row-hash (row-idx)
  (format nil "船О恂磲ъ轶灬礅溽ㄣ疳蝮瀛轭翦珏篝蜷铉悌候徜轼倍┅腩雉栳箬ㄦ矧磲铋镰立轭瘐舡篝蜷铉蝻鳝殇┅┅ㄤ彐躅溟箅栳箬ī磲疸狎＇蝻鳝栳箬ㄡ戾犷潋獒洪雉箝濯┅ㄤ彐躅痫箝糸镱躞邃痫溟箅栳箬ㄥ＼ㄣ栳铘ㄣ狎痫螬溟箅栳箬ㄣ潋痫螬┅ㄤ彐躅蝈盹鲥鲩箝翦铄鳝痫箝糸镱鲩箝翦洵痫箝糸镱螬蝈盹鲥殒灬礅溽皓礤礅弪鲩箝翦洵痫箝糸镱呼弩＇趄邋羼踽飑铄鳝痫箝糸镱螬ㄤ彐躅犰飙躞邃痫箝糸镱ㄤ轶氕栳箬镳糸镱犰痫Ж癌痫箝糸镱螬⒁弭躜犰躞邃痫箝糸镱狍泔铙沐祆螽ㄩㄡ钿ㄣ狎痫螬ō箝濯暴ㄣ潋痫螬ō箝濯暴┅痫箝糸镱戾è痫箢ㄩㄣ潋痫螬ō箝濯暴啜ǐㄣ狎痫螬暴癌啜ㄣ狎痫螬ǐㄣ潋痫螬暴┅┅ㄡ祆躞邃痫箝糸镱溟箅栳箬痫箢ㄩ痫箝糸镱躞邃痫溟箅栳箬ㄣ镱痫痫箝糸镱螬痫箝糸镱螬┅┅ㄤ彐躅珏舡铄殓桠秕蝮痫螬戾舄èㄣ狎痫螬ㄣ潋痫螬铄殓桠秕蝮啜ìǐ暴ìō暴ìǐ暴ìō暴┅┅蝈盹鲥殒灬礅溽痫螬戾èㄣ狎痫螬ㄣ潋痫螬┅矧矧窘箝濯窘箝濯┅矧癌癌┅┅铄殓桠秕蝮┅ㄤ彐躅珏舡泔铑邈翦洵铄殓桠秕蝮痫箝糸镱溟箅栳箬镳糸镱犰鲩箝翦洎⒁弭躜蝈沲蝮轹屐躞邃铄殓桠秕蝮骘痫箝糸镱螽馏篚礤痫箝糸镱翳屙箦祧弩狎躞邃戾舄è铄殓桠秕蝮ㄡ痧禊＇狃疱钿磲疸狎灬礅溽痫螬蝈盹鲥殒铒灬礅溽瞟痫箝糸镱躞邃溟箅栳箬┅ㄧ弭铄殓桠秕蝮痫螬┅痫箝糸镱螬┅铄鳝铄殓桠秕蝮蝈盹鲥鲩箝翦铄殓桠秕蝮ㄡ痧孱痫箝糸镱鲩箝翦洎┅ㄩ铛祆铄鳝铄殓桠秕蝮鲩箝翦ㄧ弭泔铑邈翦洵铄殓桠秕蝮铄鳝铄殓桠秕蝮溟箅栳箬ㄡ痧孱铄鳝铄殓桠秕蝮鲩箝翦洎┅┅ㄤ彐躅骈钿泔铑邈翦洵蝈玳镱ㄡ祆痫箝糸镱溟箅栳箬镳糸镱犰ㄡ铙癌ㄩ铛祆犰飙痫箝糸镱螬犷戾舄è沲蝌孱痫犰飙痫箝糸镱螬铄鳝铒溴ㄧ弭泔铑邈翦洵铄殓桠秕蝮啜沲蝌孱舂溟箅栳箬┅ㄦ轭洵泔铑邈翦洵蝈玳镱蝈盹鲥鲩箝翦犰飙痫箝糸镱铄鳝铒溴螬溟箅栳箬ǐ犷暴┅┅换嗅螋镱蝈漉沐＇磲疸狎灬礅溽蝻鳝栳箬蝈漉沐＇磲ъ轶灬礅溽ㄣ疳蝮瀛轭翦珏篝蜷铉悌┅蝻鳝栳箬┅ㄤ轶氕栳箬┅换嗅螋赭戾舄è溟箅栳箬ㄤ轶氕栳箬┅痫箝糸镱ㄡ祆躞邃痫箝糸镱溟箅栳箬┅ㄦ轭洵泔铑邈翦洵蝈玳镱痫箝糸镱溟箅栳箬┅